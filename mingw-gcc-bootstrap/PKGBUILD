pkgname=i386-mingw32crt-gcc-boostrap
pkgver=10.2.0
_islver=0.23
pkgrel=1
pkgdesc="Cross GCC for the MinGW32 cross-compiler (bootstrap)"
arch=('x86_64')
url="http://gcc.gnu.org"
license=('GPL' 'LGPL' 'FDL' 'custom')
groups=('i386-mingw32crt')
depends=('zlib' 'libmpc' 'i386-mingw32crt-binutils')
conflicts=('i386-mingwcrt-gcc')
options=('staticlibs' '!emptydirs')
source=(gcc-$pkgver.tar.xz
       "http://isl.gforge.inria.fr/isl-${_islver}.tar.xz")
sha512sums=('42ae38928bd2e8183af445da34220964eb690b675b1892bbeb7cd5bb62be499011ec9a93397dba5e2fb681afadfc6f2767d03b9035b44ba9be807187ae6dc65e'
	'da4e7cbd5045d074581d4e1c212acb074a8b2345a96515151b0543cbe2601db6ac2bbd93f9ad6643e98f845b68f438f3882c05b8b90969ae542802a3c78fea20')

_architectures="i386-mingw32crt"

prepare() {
  cd ${srcdir}/gcc-${pkgver}

  #do not install libiberty
  sed -i 's/install_to_$(INSTALL_DEST) //' libiberty/Makefile.in
  # hack! - some configure tests for header files using "$CPP $CPPFLAGS"
  sed -i "/ac_cpp=/s/\$CPPFLAGS/\$CPPFLAGS -O2/" {libiberty,gcc}/configure

  # link isl for in-tree builds
  ln -sf ../isl-${_islver} isl
}

build() {
  for _arch in ${_architectures}; do
    mkdir -p "${srcdir}"/gcc-build-${_arch} && cd "${srcdir}"/gcc-build-${_arch}
    "${srcdir}"/gcc-${pkgver}/configure --prefix=/usr \
        --target=${_arch} \
        --enable-languages=c,lto \
        --enable-static --disable-nls \
        --with-system-zlib \
        --enable-lto --enable-threads=win32 \
        --disable-nls --enable-version-specific-runtime-libs \
        --disable-multilib --enable-checking=release \
        --disable-sjlj-exceptions --with-dwarf2 \
	--enable-libgomp=no --disable-tls
    make all-gcc
  done
}

package() {
  for _arch in ${_architectures}; do
    cd "${srcdir}"/gcc-build-${_arch}
    make DESTDIR=${pkgdir} install-gcc
    strip ${pkgdir}/usr/bin/${_arch}-*
    strip ${pkgdir}/usr/libexec/gcc/${_arch}/${pkgver}/{cc1,collect2,lto*}
  done
  rm -r ${pkgdir}/usr/share/{man,info}
}
